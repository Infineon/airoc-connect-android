def appName = "AIROC™ Bluetooth® Connect App"
def appVersion = "1.4.0"

apply plugin: 'com.android.application'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
    }
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
        google()
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation files('libs/achartengine-1.0.0.jar')

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"

//	  testImplementation 'junit:junit:4.12'
//	  androidTestImplementation 'com.android.support.test:runner:1.0.2'
//	  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

android {
    compileSdkVersion 31
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    defaultConfig {
        minSdkVersion 28
        targetSdkVersion 31

        // BUILD_NUMBER is set by build.sh
        def buildNumber = "${System.getenv('BUILD_NUMBER') ?: 1}".toInteger()
        def version = "${appVersion}.${buildNumber}"
        versionName "$version"
        versionCode buildNumber

        // Remove non-ascii characters and spaces
        def appNameAscii = appName.replaceAll("\\P{Print}", "").replaceAll(" ", "_");
        setProperty("archivesBaseName", "${appNameAscii}_${version}")
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    productFlavors {
    }
}
